scp_program-> proc_of_checking_set_of_semantic_neighbourhood
(*
	-> rrel_params: .proc_of_checking_set_params
	(*
	-> rrel_1: rrel_in: _set_of_node;;
	-> rrel_2: rrel_in: _answer;;
	*);;

	-> rrel_operators: .proc_of_checking_set_of_semantic_neighbourhood_set
	(*
	-> rrel_init: .proc_of_checking_set_of_semantic_neighbourhood_operator1A
		(*
		<- searchElStr3;;

		-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_node;;
		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		-> rrel_3: rrel_assign: rrel_scp_var: _node;;

		=> nrel_then: .proc_of_checking_set_of_semantic_neighbourhood_operator1A1;;
		=> nrel_else: .proc_of_checking_set_of_semantic_neighbourhood_return;;
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1A1
		(*
		<- eraseEl;;

		-> rrel_1: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: rrel_erase: _arc1;;

		=> nrel_goto: .proc_of_checking_set_of_semantic_neighbourhood_operator1A2;;
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1A2
		(*
		<- searchSetStr3;;

		-> rrel_1: rrel_assign: rrel_scp_var: _element;;
		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		-> rrel_3: rrel_fixed: rrel_scp_var: _node;;

		-> rrel_set_1: rrel_assign: rrel_scp_var: _set_of_element;;

		=> nrel_then: .proc_of_checking_set_of_semantic_neighbourhood_operator1B1;;
		=> nrel_else: .proc_of_checking_set_of_semantic_neighbourhood_operator1A3;; 
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1B1
		(*
		<- searchElStr3;;

		-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_element;;
		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		-> rrel_3: rrel_assign: rrel_scp_var: _element;;

		=> nrel_then: .proc_of_checking_set_of_semantic_neighbourhood_operator1B2;;
		=> nrel_else: .proc_of_checking_set_of_semantic_neighbourhood_operator1A3;; 
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1B2
		(*
		<- eraseEl;;

		-> rrel_1: rrel_fixed: rrel_scp_var: rrel_pos_const_perm: rrel_erase: _arc1;;

		=> nrel_goto: .proc_of_checking_set_of_semantic_neighbourhood_operator1B3;;
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1B3
		(*
		<- searchElStr3;;

		-> rrel_1: rrel_fixed: rrel_scp_const: semantic_neighbourhood;;
		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		-> rrel_3: rrel_fixed: rrel_scp_var: _element;;

		=> nrel_then: .proc_of_checking_set_of_semantic_neighbourhood_operator1A;;
		=> nrel_else: .proc_of_checking_set_of_semantic_neighbourhood_operator1B1;;
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_operator1A3
		(*
		<- genElStr3;;

		-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		-> rrel_3: rrel_fixed: rrel_scp_var: _node;;

		=> nrel_goto: .proc_of_checking_set_of_semantic_neighbourhood_operator1A;;
		*);;

	-> .proc_of_checking_set_of_semantic_neighbourhood_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;