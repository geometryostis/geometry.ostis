agent_of_creating_draft
=> nrel_main_idtf:
	[агентная scp-программа создания черновика] (* <- lang_ru;; *);
	[agent scp-program of creating a draft] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_creating_draft 
	(*
	-> rrel_params: .agent_of_creating_draft_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_creating_draft_operator_set
		(*
		-> rrel_init: .agent_of_creating_draft_operatorA 
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

		 	=> nrel_then: .agent_of_creating_draft_operatorB;;
			=> nrel_else: .agent_of_creating_draft_operator_return;;
			*);;

		
		-> .agent_of_creating_draft_operatorB
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_of_creating_draft;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_creating_draft_operatorC;;
			=> nrel_else: .agent_of_creating_draft_operator_return;;
			*);;
		
		-> .agent_of_creating_draft_operatorC
                 	(*
		 	<- searchElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_const: ui_user;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _user;;

		 	=> nrel_then: .agent_of_creating_draft_operatorD1;;
			=> nrel_else: .agent_of_creating_draft_operator_return;;
		 	*);; 

		//Проверяется наличие связки отношения исполнитель*, связывающей знак пользователя ostis-системы, инициировавшего запрос, и 			знак деятельности этого пользователя

		-> .agent_of_creating_draft_operatorD1
                 	(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _user;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _activity;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_performer;;

		 	=> nrel_then: .agent_of_creating_draft_operatorE1;;
			=> nrel_else: .agent_of_creating_draft_operatorD2;;
		 	*);; 

		-> .agent_of_creating_draft_operatorD2
                 	(*
		 	<- genElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _user;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _activity;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_performer;;

		 	=> nrel_goto: .agent_of_creating_draft_operatorD3;;
		 	*);; 

		-> .agent_of_creating_draft_operatorD3
                 	(*
		 	<- genElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _activity;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_private_action;;

		 	=> nrel_goto: .agent_of_creating_draft_operatorE1;;
		 	*);; 

		//Проверяется наличие связки отношения блокировка*, связывающей знак деятельности пользователя и знак полной блокировки 

		-> .agent_of_creating_draft_operatorE1
                 	(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _activity;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _block_activity;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_lock;;

		 	=> nrel_then: .agent_of_creating_draft_operatorF1;;
			=> nrel_else: .agent_of_creating_draft_operatorE2;;
		 	*);; 

		-> .agent_of_creating_draft_operatorE2
                 	(*
		 	<- genEl;;

		 	-> rrel_1: rrel_assign: rrel_scp_var: rrel_structure: rrel_node: _block_activity;;

		 	=> nrel_goto: .agent_of_creating_draft_operatorE3;;
		 	*);; 

		-> .agent_of_creating_draft_operatorE3
                 	(*
		 	<- genElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _activity;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _block_activity;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_lock;;

		 	=> nrel_goto: .agent_of_creating_draft_operatorE4;;
		 	*);; 

		-> .agent_of_creating_draft_operatorE4
                 	(*
		 	<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_const: total_lock;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _block_activity;;
	
		 	=> nrel_goto: .agent_of_creating_draft_operatorF1;;
		 	*);; 

		//В рамках содержимого знака полной блокировки создаётся знак нового черновика

		-> .agent_of_creating_draft_operatorF1
                 	(*
		 	<- genEl;;

		 	-> rrel_1: rrel_assign: rrel_scp_var: rrel_structure: rrel_node: _draft;;

		 	=> nrel_goto: .agent_of_creating_draft_operatorF2;;
		 	*);;
	
		-> .agent_of_creating_draft_operatorF2
                 	(*
		 	<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _block_activity;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _draft;;
	
		 	=> nrel_goto: .agent_of_creating_draft_operatorF3;;
		 	*);; 

		-> .agent_of_creating_draft_operatorF3
                 	(*
		 	<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_const: draft;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _draft;;
	
		 	=> nrel_goto: .agent_of_creating_draft_operatorG1;;
		 	*);; 

		//Генерация связки отношения автор*, связывающая знак пользователя ostis-системы, инициировавшего запрос, и знак созданного 			черновика

		-> .agent_of_creating_draft_operatorG1
                 	(*
		 	<- genElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _user;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _draft;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_author;;

		 	=> nrel_goto: .agent_of_creating_draft_operator_return;;
		 	*);; 

		-> .agent_of_creating_draft_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;


