scp_program -> proc_recursive_count_percent_of_temporary_entity
	(*
  	-> rrel_params: .proc_recursive_count_percent_of_temporary_entity_params 
		(*
		-> rrel_1: rrel_in: _set;;
		-> rrel_2: rrel_in: _number_of_entity;;
		-> rrel_3: rrel_in: _set_of_elems;;
    	*);;

	-> rrel_operators: .proc_recursive_count_percent_of_temporary_entity_operator_set
    		(*	
    		-> rrel_init: .proc_recursive_count_percent_of_temporary_entity_operator_increase_number 
			(*
	        <- contAdd;;

	        -> rrel_1: rrel_fixed: rrel_scp_var: _number_of_entity;;
	        -> rrel_2: rrel_fixed: rrel_scp_var: _number_of_entity;;
			-> rrel_3: rrel_fixed: rrel_scp_const: [1];;
	         		
	        => nrel_goto: .proc_recursive_count_percent_of_temporary_entity_operator_finding_subset;;
			*);;



    		-> .proc_recursive_count_percent_of_temporary_entity_operator_finding_subset 
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _element;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _all_elements;;

			=> nrel_then: .proc_recursive_count_percent_of_temporary_entity_operator_take_entity_from_subset;;
			=> nrel_else: .proc_of_cardinality_calculation_operator_return;;
			*);;



		-> .proc_recursive_count_percent_of_temporary_entity_operator_take_entity_from_subset
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _all_elements;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _element;;

			=> nrel_then: .proc_recursive_count_percent_of_temporary_entity_operator_checking_in_visited;;
			=> nrel_else: .proc_recursive_count_percent_of_temporary_entity_operator_clean_garbage;;
			*);;

		-> .proc_recursive_count_percent_of_temporary_entity_operator_checking_in_visited
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_elems;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _ar;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _element;;

			=> nrel_then: .proc_of_cardinality_calculation_operator_return;;
			=> nrel_else: .proc_recursive_count_percent_of_temporary_entity_operator_adding_to_visited;;
			*);;

		-> .proc_recursive_count_percent_of_temporary_entity_operator_adding_to_visited
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_elems;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _ar;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _element;;

			=> nrel_goto: .proc_recursive_count_percent_of_temporary_entity_operator_loop_arc_erase;;
			*);;




		-> .proc_recursive_count_percent_of_temporary_entity_operator_loop_arc_erase
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc1;;

			=> nrel_goto: .proc_recursive_count_percent_of_temporary_entity_operator_recursive_call;;
			*);;



		-> .proc_recursive_count_percent_of_temporary_entity_operator_recursive_call
			(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_recursive_count_percent_of_temporary_entity;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .proc_recursive_count_percent_of_temporary_entity_operator_recursive_call_params
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _element;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _number_of_entity;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _set_of_elems;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_recursive_count_percent_of_temporary_entity_operator_wait_return;;
			*);;	

		-> .proc_recursive_count_percent_of_temporary_entity_operator_wait_return
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_recursive_count_percent_of_temporary_entity_operator_take_entity_from_subset;;
			*);;	



		-> .proc_recursive_count_percent_of_temporary_entity_operator_clean_garbage
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _all_elements;;

			=> nrel_goto: .proc_of_cardinality_calculation_operator_return;;
			*);;	

		-> .proc_of_cardinality_calculation_operator_return 
			(*
			<- return;;
			*);;
		*);;
	*);;
