scp_program -> proc_of_calculation_depth_sum
	(*
	-> rrel_params: .proc_of_calculation_depth_sum_params
		(*
		-> rrel_1: rrel_in: _subject_domain;; 
		-> rrel_2: rrel_in: _current_depth;; 
		-> rrel_3: rrel_in: _all_depth;;
		*);;

	-> rrel_operators: .proc_of_calculation_depth_sum_operator_set
		(*
		-> rrel_init: .proc_of_calculation_depth_sum_operator1
				(*
				<- contAdd;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var: _current_depth;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _current_depth;;
				-> rrel_3: rrel_fixed: rrel_scp_const: [1];;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator2;;
				*);;
		
		-> .proc_of_calculation_depth_sum_operator2
				(*
				<- genEl;;			
				
				-> rrel_1: rrel_assign: rrel_scp_var: rrel_node:  _last_level_vertexes;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator3;;
				*);;

		-> .proc_of_calculation_depth_sum_operator3
				(*
				<- searchSetStr5;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _subject_domain;;
				-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
				-> rrel_3: rrel_assign: rrel_scp_var:  _elem;;
				-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
				-> rrel_5: rrel_fixed: rrel_scp_const: nrel_private_subject_domain;;

				-> rrel_set_3: rrel_fixed: rrel_scp_var: _last_level_vertexes;;

				=> nrel_then: .proc_of_calculation_depth_sum_operator4;;
				=> nrel_else: .proc_of_calculation_depth_sum_return;;
				*);;

		-> .proc_of_calculation_depth_sum_operator4
				(*
				<- contAssign;;			
				
				-> rrel_1: rrel_assign: rrel_scp_var:  _is_last_level;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [true];;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator5;;
				*);;

		-> .proc_of_calculation_depth_sum_operator5
				(*
				<- searchElStr3;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _last_level_vertexes;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
				-> rrel_3: rrel_assign: rrel_scp_var:  _curr_elem;;

				=> nrel_then: .proc_of_calculation_depth_sum_operator6;;
				=> nrel_else: .proc_of_calculation_depth_sum_operator11;;
				*);;

		-> .proc_of_calculation_depth_sum_operator6
				(*
				<- eraseEl;;			
				
				-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: rrel_pos_const_perm: _arc1;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator7;;
				*);;

		-> .proc_of_calculation_depth_sum_operator7
				(*
				<- contAssign;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _is_last_level;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [false];;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator8;;
				*);;

		-> .proc_of_calculation_depth_sum_operator8
				(*
				<- call;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_const:  proc_of_calculation_depth_sum;;
				-> rrel_2: rrel_fixed: rrel_scp_const: .proc_of_calculation_depth_sum_operator8_params
						(*
						-> rrel_1: rrel_fixed: rrel_scp_var: _curr_elem;;

						-> rrel_2: rrel_fixed: rrel_scp_var: _current_depth;; 

						-> rrel_3: rrel_fixed: rrel_scp_var: _all_depth;;
						*);;
				-> rrel_3: rrel_assign: rrel_scp_var:  _descr;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator9;;
				*);;

		-> .proc_of_calculation_depth_sum_operator9
				(*
				<- waitReturn;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _descr;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator10;;
				*);;

		-> .proc_of_calculation_depth_sum_operator10
				(*
				<- contSub;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _current_depth;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _current_depth;;
				-> rrel_3: rrel_fixed: rrel_scp_const: [1];;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator5;;
				*);;

		-> .proc_of_calculation_depth_sum_operator11
				(*
				<- ifEq;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _is_last_level;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [true];;

				=> nrel_then: .proc_of_calculation_depth_sum_operator12;;
				=> nrel_else: .proc_of_calculation_depth_sum_operator14;;
				*);;

		-> .proc_of_calculation_depth_sum_operator12
				(*
				<- contAssign;;			
				
				-> rrel_1: rrel_assign: rrel_scp_var:  _depth;;
				-> rrel_2: rrel_fixed:  rrel_scp_var:  _current_depth;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator131;;
				*);;

		-> .proc_of_calculation_depth_sum_operator131
			(*
			<- printNl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _current_depth;;
		
			=> nrel_goto: .proc_of_calculation_depth_sum_operator13;;
			*);;

		-> .proc_of_calculation_depth_sum_operator13
				(*
				<- genElStr3;;			
				
				-> rrel_1: rrel_fixed: rrel_scp_var:  _all_depth;;
				-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
				-> rrel_3: rrel_fixed: rrel_scp_var:  _depth;;

				=> nrel_goto: .proc_of_calculation_depth_sum_operator14;;
				*);;

		-> .proc_of_calculation_depth_sum_operator14
				(*
				<- eraseEl;;			
				
				-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var:  _last_level_vertexes;;

				=> nrel_goto: .proc_of_calculation_depth_sum_return;;
				*);;

		->.proc_of_calculation_depth_sum_return
				(*
				<- return;;
				*);;	
		*);;
	*);;
